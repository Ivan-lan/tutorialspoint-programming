# Fortran动态数组 - Fortran教程

动态数组是一种数组，其尺寸在编译时不知道，而是在执行时才已知/确定的。

动态数组的属性使用 allocatable 声明。 .

例如，

```
real, dimension (:,:), allocatable :: darray    
```

数组的秩，即尺寸，必须提到但是，分配内存以这样的阵列，可以使用allocate函数。

```
allocate ( darray(s1,s2) )      
```

该阵列使用后，在该程序中，所创建的存储器应该使用 deallocate 函数解除

```
deallocate (darray)  
```

**示例**

下面的例子演示了上面讨论的概念。

```
program dynamic_array 
implicit none 

   !rank is 2, but size not known   
   real, dimension (:,:), allocatable :: darray    
   integer :: s1, s2     
   integer :: i, j     

   print*, "Enter the size of the array:"     
   read*, s1, s2      

   ! allocate memory      
   allocate ( darray(s1,s2) )      

   do i = 1, s1           
      do j = 1, s2                
         darray(i,j) = i*j               
         print*, "darray(",i,",",j,") = ", darray(i,j)           
      end do      
   end do      

   deallocate (darray)  
end program dynamic_array
```

当上述代码被编译和执行时，它产生了以下结果：

```
Enter the size of the array: 3,4
darray( 1 , 1 ) = 1.00000000    
darray( 1 , 2 ) = 2.00000000    
darray( 1 , 3 ) = 3.00000000    
darray( 1 , 4 ) = 4.00000000    
darray( 2 , 1 ) = 2.00000000    
darray( 2 , 2 ) = 4.00000000    
darray( 2 , 3 ) = 6.00000000    
darray( 2 , 4 ) = 8.00000000    
darray( 3 , 1 ) = 3.00000000    
darray( 3 , 2 ) = 6.00000000    
darray( 3 , 3 ) = 9.00000000    
darray( 3 , 4 ) = 12.0000000   

```

## 使用data语句

data 语句可用于初始化多个阵列，或用于阵列部分的初始化。

data 语句的语法是：

```
data variable / list / ...
```

**示例**

下面的例子演示了这一概念：

```
program dataStatement
implicit none

   integer :: a(5), b(3,3), c(10),i, j
   data a /7,8,9,10,11/ 

   data b(1,:) /1,1,1/ 
   data b(2,:)/2,2,2/ 
   data b(3,:)/3,3,3/ 
   data (c(i),i=1,10,2) /4,5,6,7,8/ 
   data (c(i),i=2,10,2)/5*2/

   Print *, 'The A array:'
   do j = 1, 5                
      print*, a(j)           
   end do 

   Print *, 'The B array:'
   do i = lbound(b,1), ubound(b,1)
      write(*,*) (b(i,j), j = lbound(b,2), ubound(b,2))
   end do

   Print *, 'The C array:' 
   do j = 1, 10                
      print*, c(j)           
   end do      

end program dataStatement
```

当上述代码被编译和执行时，它产生了以下结果：

```
The A array:
7
8
9
10
11
The B array:
1  1  1
2  2  2
3  3  3
The C array:
4
2
5
2
6
2
7
2
8
2

```

## 使用where语句

where语句可以使用数组中的某些元素在一个表达式，根据一些逻辑条件的结果。它允许表达的执行在一个元素上，如果给定的条件为真。

**例子**

下面的例子演示了这一概念：

```
program whereStatement
implicit none

   integer :: a(3,5), i , j

   do i = 1,3
      do j = 1, 5                
         a(i,j) = j-i          
      end do 
   end do

   Print *, 'The A array:'

   do i = lbound(a,1), ubound(a,1)
      write(*,*) (a(i,j), j = lbound(a,2), ubound(a,2))
   end do

   where( a<0 ) 
      a = 1 
   elsewhere
      a = 5
   end where

   Print *, 'The A array:'
   do i = lbound(a,1), ubound(a,1)
      write(*,*) (a(i,j), j = lbound(a,2), ubound(a,2))
   end do   

end program whereStatement
```

当上述代码被编译和执行时，它产生了以下结果：

```
The A array:
0   1   2  3  4
-1  0   1  2  3
-2  -1  0  1  2
The A array:
5   5   5  5  5
1   5   5  5  5
1   1   5  5  5
```

 