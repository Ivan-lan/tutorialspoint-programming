# D语言元组 - D语言教程

元组用于组合多个值作为单个对象。元组包含的元素的序列。元素可以是类型，表达式或别名。元组的数目和元件固定在编译时，它们不能在运行时改变。

元组有两个结构和数组的特性。元组元素可以是不同的类型，如结构的。该元素可以通过索引数组一样访问。它们是由从std.typecons模块的元组模板实现为一个库功能。元组利用TypeTuple从一些业务的std.typetuple模块。

## 使用元组tuple()

元组可以由函数tuple()来构造。一个元组的成员由索引值访问。一个例子如下所示。

```
import std.stdio;
import std.typecons;

void main()
{
   auto myTuple = tuple(1, "Tuts");
   writeln(myTuple);
   writeln(myTuple[0]);
   writeln(myTuple[1]);
}
```

当上面的代码被编译并执行，它会产生以下结果：

```
Tuple!(int, string)(1, "Tuts")
1
Tuts

```

## 使用元组元组模板

元组也可以由元组模板而不是tuple()函数直接构造。每个成员的类型和名称被指定为两个连续的模板参数。它可以通过属性使用模板创建的时候访问的成员。

```
import std.stdio;
import std.typecons;

void main()
{
   auto myTuple = Tuple!(int, "id",string, "value")(1, "Tuts");
   writeln(myTuple);

   writeln("by index 0 : ", myTuple[0]);
   writeln("by .id : ", myTuple.id);

   writeln("by index 1 : ", myTuple[1]);
   writeln("by .value ", myTuple.value);
}
```

当上面的代码被编译并执行，它会产生以下结果：

```
Tuple!(int, "id", string, "value")(1, "Tuts")
by index 0 : 1
by .id : 1
by index 1 : Tuts
by .value Tuts

```

## 扩展属性和函数参数

元组的成员可以通过.expand扩展属性或通过切片进行扩展。这种扩展/切片值可以作为函数的参数列表。一个例子如下所示。

```
import std.stdio;
import std.typecons;
void method1(int a, string b, float c, char d)
{
   writeln("method 1 ",a,"	",b,"	",c,"	",d);
}
void method2(int a, float b, char c)
{
   writeln("method 2 ",a,"	",b,"	",c);
}
void main()
{
   auto myTuple = tuple(5, "my string", 3.3, 'r');

   writeln("method1 call 1");
   method1(myTuple[]);

   writeln("method1 call 2");
   method1(myTuple.expand);

   writeln("method2 call 1");
   method2(myTuple[0], myTuple[$-2..$]);
}
```

当上面的代码被编译并执行，它会产生以下结果：

```
method1 call 1
method 1 5	my string	3.3	r
method1 call 2
method 1 5	my string	3.3	r
method2 call 1
method 2 5	3.3	r

```

## TypeTuple

TypeTuple在std.typetuple模块中定义。值和类型的逗号分隔的列表。使用TypeTuple一个简单的例子如下。 TypeTuple用于创建参数列表，模板列表和数组文本列表。

```
import std.stdio;
import std.typecons;
import std.typetuple;

alias TypeTuple!(int, long) TL;

void method1(int a, string b, float c, char d)
{
   writeln("method 1 ",a,"	",b,"	",c,"	",d);
}
void method2(TL tl)
{
   writeln(tl[0],"	", tl[1] );
}

void main()
{
   auto arguments = TypeTuple!(5, "my string", 3.3,'r');

   method1(arguments);

   method2(5, 6L);

}
```

当上面的代码被编译并执行，它会产生以下结果：

```
method 1 5	my string	3.3	r
5	6
```

 